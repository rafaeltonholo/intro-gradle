/*

Copiar arquivos com Gradle é simples. Começamos simplesmente declarando uma nova task
e especificamos o "type" como "Copy".

*/

task copyTask(type: Copy)

/*

Obviamente, esta task não fará nada, pois não dissemos que arquivo deve ser copiado
e nem para onde. O exemplo mais simples é simplesmente copiar o conteúdo de uma pasta
para outra. Nós usamos o método "from" para adicionar fontes e método "into" para 
especificar o destino. Esta task copiará os conteúdos da pasta "images" para a pasta
"build"

*/

task copyImages(type: Copy) {
    from 'images'
    into 'build'
}


/*

Podemos especificar arquivos para incluir ou excluir. Nossos filtros podem
utilizar expressões para incluir vários arquivos ao mesmo tempo.
Vamos configurar nossa task para incluir somente as imagens JPEG 
chamando o método "include" e passando um padrão de arquivos.

*/

task copyJpegs(type: Copy) {
    from 'images'
    include '*.jpg'
    into 'build'
}

/*

Podemos também configurar uma simples task Copy para copiar de múltiplas fontes.
Além disso, as especificações podem ser hierárquicas.

Vamos configurar uma task Copy que copia todos os JPEGS em uma pasta e os GIFS em outra.

*/

task copyImageFolders(type: Copy) {
    from('images') {
        include '*.jpg'
        into 'jpeg'
    }

    from('images') {
        include '*.gif'
        into 'gif'
    }

    into 'build'
}

/*


Gradle fornece também task type para criar arquivos compactados como Zip e Tar.


*/

task zipImages(type: Zip) {
    baseName = 'images'
    destinationDir = file('build')
    from 'images'
}


task zipImageFolders(type: Zip) {
    baseName = 'images'
    destinationDir = file('build')

    from('images') {
        include '*.jpg'
        into 'jpeg'
    }

    from('images') {
        include '*.gif'
        into 'gif'
    }
}

/*

Para deletar arquivos, utilizamos o task type "Delete"

*/

task deleteBuild(type: Delete) {
    delete 'build'
}
